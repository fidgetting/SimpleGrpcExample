
plugins {
  id 'application'
  id 'java'
  alias libs.plugins.protobuf
  alias libs.plugins.jooq
}

repositories {
  mavenCentral()
  google()
  maven {
    url = "https://mvnrepository.com/artfiact/com.zaxxer/HikariCP"
  }
}

group = 'com.github.fidgetting'
version = '0.0.1'

application {
  mainClassName = 'com.github.fidgetting.Main'
}

dependencies {
  implementation libs.bundles.database
  implementation libs.bundles.protobuf
  implementation libs.bundles.logging
  implementation libs.apacheAnnotations

  jooqGenerator libs.postgresqlJDBC
}

protobuf {
  protoc {
    artifact = "com.google.protobuf:protoc:${libs.versions.protobuf.get()}"
  }
  plugins {
    grpc {
      artifact = "io.grpc:protoc-gen-grpc-java:${libs.versions.grpc.get()}"
    }
    reactor {
      artifact = "com.salesforce.servicelibs:reactor-grpc:${libs.versions.reactorGrpc.get()}"
    }
  }
  generateProtoTasks {
    all().forEach {
      it.plugins {
        grpc { }
        reactor { }
      }
      it.builtins {
        java { }
      }
    }
  }
}

jooq {
  configurations {
    main {
      generationTool {
        logging = org.jooq.meta.jaxb.Logging.WARN
        jdbc {
          driver = "org.postgresql.Driver"
          url = "jdbc:postgresql://localhost:5433/postgres"
          user= "anorton"
          password = "anorton"
        }
        generator {
          name = "org.jooq.codegen.JavaGenerator"
          database {
            name = "org.jooq.meta.postgres.PostgresDatabase"
            inputSchema = "public"
          }
          target {
            packageName = "com.github.fidgetting.schema"
          }
        }
      }
    }
  }
}
